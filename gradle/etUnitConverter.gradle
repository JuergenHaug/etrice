configurations {
	etunit
}

dependencies {
	etunit project(':plugins:org.eclipse.etrice.etunit.converter')
}

/**
 * Creates a tasks that converts etu files to xml files
 * @param name: The name of the new task
 * @param etuFiles: A list of etu files
 * @param options: The options passed to the converter
 */
ext.createEtUnitConverterTask = { name, etuFiles, options = [] ->
	def xmlFiles = etuFiles.collect { it.replace('.etu', '.xml') }

	return tasks.create(name: name, type: JavaExec) {
		main = 'org.eclipse.etrice.etunit.converter.EtUnitReportConverter'
		classpath = configurations.etunit
		args options
		args etuFiles
		inputs.files etuFiles
		outputs.files xmlFiles
	}
}