/*******************************************************************************
 * Copyright (c) 2012 protos software gmbh (http://www.protos.de).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * 
 * CONTRIBUTORS:
 * 		Juergen Haug and Thomas Schuetz (initial contribution)
 * 		Eyrak Paen
 * 
 *******************************************************************************/
grammar org.eclipse.etrice.core.Config with org.eclipse.etrice.core.common.Base

generate config "http://www.eclipse.org/etrice/core/Config"
import "http://www.eclipse.org/etrice/core/common/Base"
import "http://www.eclipse.org/etrice/Room" as room

ConfigModel:
	'ConfigModel' name=FQN '{'
	imports+=Import*
	configElements+=ConfigElement*
	'}';

ConfigElement:
	SubSystemConfig | ActorClassConfig | ActorInstanceConfig | ProtocolClassConfig;

SubSystemConfig:
	'SubSystemConfig' root=[room::LogicalSystem|FQN] '/' subSystem=[room::SubSystemRef|ID] '{'
	dynConfig=DynamicConfig
	'}';

DynamicConfig:
	'dynamic configuration' '{'
	((('file path' filePath=STRING) |
	(('user import' userCode1=STRING)
	('user constructor' userCode2=STRING))) &
	('polling timer [ms]' polling=INT)?)
	'}';

ActorClassConfig:
	'ActorClassConfig' actor=[room::ActorClass|FQN] '{'
	(attributes+=AttrClassConfig)*
	'}';

ActorInstanceConfig:
	'ActorInstanceConfig' root=[room::LogicalSystem|FQN] '/' subSystem=[room::SubSystemRef|ID] '/' path=RefPath '{'
	((attributes+=AttrInstanceConfig)* &
	(ports+=PortInstanceConfig)*)
	'}';

ProtocolClassConfig:
	'ProtocolClassConfig' protocol=[room::ProtocolClass|FQN] '{'
	(('regular' regular=PortClassConfig)? &
	('conjugate' conjugated=PortClassConfig)?)
	'}';

PortClassConfig:
	{PortClassConfig} 'Port' '{'
	(attributes+=AttrClassConfig)*
	'}';

PortInstanceConfig:
	'InterfaceItem' item=[room::InterfaceItem|ID] '{'
	(attributes+=AttrInstanceConfig)*
	'}';

AttrConfig:
	AttrClassConfig | AttrInstanceConfig;

AttrClassConfig:
	'Attr' attribute=[room::Attribute] ('=' value=LiteralArray)? ('{'
	(('min' '=' min=NumberLiteral)?
	('max' '=' max=NumberLiteral)?
	(attributes+=AttrClassConfig)*)
	'}')?;

AttrInstanceConfig:
	'Attr' attribute=[room::Attribute] ('=' value=LiteralArray)? ('{'
	((dynConfig?='dynamic configuration' (readOnly?='read' | 'write'))?
	(attributes+=AttrInstanceConfig*))
	'}')?;

RefPath:
	refs+=ID ('/' refs+=ID)*;

	// -------------------- from org.eclipse.etrice.core.Room.xtext ---------------
Import:
	'import' (importedNamespace=ImportedFQN 'from' | 'model') importURI=STRING;

ImportedFQN:
	FQN ('.*')?;

FQN:
	ID ('.' ID)*;

	// -----------------------------------------------------------------------------

//enum Unit:
//	N|
//	W|
//	S = 's'|
//	KG = 'kg'|
//	M = 'm'|
//	C|
//	NM|
//	RPM|
//	V|
//	A|
//	HZ = 'Hz'|
//	KW
//;

