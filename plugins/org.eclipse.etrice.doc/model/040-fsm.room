RoomModel FiniteStateMachines {
	ActorClass FlatFSM {
		Interface {
			Port fct: ThisAndThat
		}
		Structure {
			external Port fct
		}
		Behavior {
			StateMachine {
				Transition init: initial -> Initial { }
				Transition tr0: Initial -> DoingThis {
					triggers {
						<doThis: fct>
					}
				}
				Transition tr1: DoingThis -> DoingThat {
					triggers {
						<doThat: fct>
					}
				}
				Transition tr2: my hdlErr -> Error {
					triggers {
						<doThat: fct>
					}
				}
				Transition tr3: Error -> Initial {
					triggers {
						<reset: fct>
					}
				}
				TransitionPoint hdlErr
				State Initial
				State DoingThis
				State DoingThat
				State Error
			}
		}
	}
	
	ActorClass HierarchicalFSM {
		Interface {
			Port fct: ThisAndThat
		}
		Structure {
			external Port fct
		}
		Behavior {
			StateMachine {
				Transition init: initial -> Initializing { }
				Transition tr1: my errHdlr -> reInit of Initializing {
					triggers {
						<reset: fct>
					}
				}
				Transition tr0: tp0 of Initializing -> Running
				TransitionPoint errHdlr
				State Initializing {
					subgraph {
						Transition init: initial -> Phase1 { }
						Transition tr0: Phase1 -> Phase2 {
							triggers {
								<doThis: fct>
							}
						}
						Transition tr1: my reInit -> Phase1
						Transition tr2: Phase2 -> my tp0 {
							triggers {
								<doThat: fct>
							}
						}
						EntryPoint reInit
						ExitPoint tp0
						State Phase1
						State Phase2
					}
				}
				State Running {
					subgraph {
						Transition init: initial -> Process { }
						State Process
					}
				}
			}
		}
	}
	
	ProtocolClass ThisAndThat {
		incoming {
			Message doThis()
			Message doThat()
			Message reset()
		}
		outgoing {
			Message doneThis()
			Message doneThat()
			Message error()
		}
	}
}