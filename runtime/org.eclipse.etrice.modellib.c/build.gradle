plugins {
	id 'c'
}

def generator = ':plugins:org.eclipse.etrice.generator.c'

task generate(type: JavaExec, group: 'build', dependsOn: "$generator:classes") {
	main = project(generator).mainClassName
	classpath = project(generator).classpath
	inputs.dir 'model'
	outputs.dir 'src-gen'
	args '-lib', '-msc_instr', '-genDir', 'src-gen',
		'model/PInterrupt.room',
		'model/TcpService.room',
		'model/TimingService.room'
}

model {
	components {
		etrice_modellib_c(NativeLibrarySpec) {
			sources.c {
				builtBy generate
				source {
					srcDirs = ['src-gen']
					include '**/*.c'
				}
				exportedHeaders {
					srcDirs = ['src-gen']
				}
				lib project: ':runtime:org.eclipse.etrice.runtime.c', library: 'etrice_runtime_c', linkage: 'api'
			}
			binaries {
				withType(SharedLibraryBinarySpec) { buildable = false }
				all { cCompiler.args '-g3' }
			}
		}
	}
}

clean.delete 'src-gen'