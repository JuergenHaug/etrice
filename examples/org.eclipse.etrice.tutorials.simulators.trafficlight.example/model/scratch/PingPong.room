RoomModel PingPong {

	import room.basic.service.timing.* from "../../../org.eclipse.etrice.modellib.java/model/TimingService.room"

	LogicalSystem System_PingPong {
		SubSystemRef subsystem: SubSystem_PingPong
	}

	SubSystemClass SubSystem_PingPong {
		ActorRef application: PingPongTop
		ActorRef services: ATimingService
		LayerConnection ref application satisfied_by services.timer
		LogicalThread mrPingThread 
		LogicalThread mrPong1Thread
		ActorInstanceMapping application/MrPing -> mrPingThread
		ActorInstanceMapping application/MrPong1 -> mrPong1Thread 
	}

	// automatically inserted App_SubSystem_PedLightController
	ActorClass PingPongTop {
		Structure {
			ActorRef MrPing: MrPingActor
			ActorRef MrPong1: MrPongActor1
	//		ActorRef MrPong2: MrPongActor2
			Binding MrPing.PingPongPort and MrPong1.PingPongPort
		}
	}

	ActorClass MrPingActor {
		Interface {
			conjugated Port PingPongPort [2]: PingPongProtocol
		}
		Structure {
			external Port PingPongPort 
			SAP timer: PTimer
		}
		Behavior {
			StateMachine {
				Transition tr0: initial -> waitForTimer {
					action {
						"timer.startTimeout(1000);"
					}
				}
				Transition tr1: waitForTimer -> waitForPong {
					triggers {
						<timeout: timer>
					}
					action {
						"PingPongPort.ping();"
					}
				}
				Transition tr2: waitForPong -> waitForTimer {
					triggers {
						<pong: PingPongPort>
					}
					action {
						"timer.startTimeout(1000);"
					}
				}
				State waitForTimer { }
				State waitForPong { }
			}
		}
	}

	ActorClass MrPongActor1 {
		Interface {
			Port PingPongPort: PingPongProtocol
		}
		Structure {
			external Port PingPongPort
		}
		Behavior {
			StateMachine {
				Transition tr0: initial -> waitForPing { }
				Transition myTrigger: waitForPing -> waitForPing {
					triggers {
						<ping: PingPongPort>
					}
					action {
						"PingPongPort.pong();"
					}
				}
				State waitForPing { }
			}
		}
	}
	
/*	ActorClass MrPongActor2 {
		Interface {
			Port PingPongPort: PingPongProtocol
		}
		Structure {
			external Port PingPongPort
		}
		Behavior {
			StateMachine {
				Transition tr0: initial -> waitForPing { }
				Transition myTrigger: waitForPing -> waitForPing {
					triggers {
						<ping: PingPongPort>
					}
					action {
						"PingPongPort.pong();"
					}
				}
				State waitForPing { }
			}
		}
	}*/
	
	ProtocolClass PingPongProtocol {
		incoming {
			Message ping()
		}
		outgoing {
			Message pong()
		}
	}

}